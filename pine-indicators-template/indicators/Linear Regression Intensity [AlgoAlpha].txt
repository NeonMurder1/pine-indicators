// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © AlgoAlpha
//@version=6
indicator("Linear Regression Intensity [AlgoAlpha]", shorttitle="AlgoAlpha - Regression Intensity", overlay=true, max_labels_count = 500)

// Input Groups
group_calc = "Calculation Parameters"
group_appearance = "Appearance Settings"
group_alerts = "Alert Conditions"

// Inputs with Tooltips
n = input.int(12, title="Lookback Period", minval=1, group=group_calc, 
     tooltip="Number of historical price points to analyze for trend intensity. Higher values provide more comprehensive trend analysis.")

p = input.int(90, title="Range Tolerance", minval=0, maxval=100, group=group_calc, 
     tooltip="Percentage threshold for determining trend strength. Higher values require more consistent trend alignment.")

linreg_source = input.source(close, title="Linear Regression Source", group=group_calc, 
     tooltip="Price source used for calculating the linear regression trend.")

linreg_length = input.int(90, title="Linear Regression Length", minval=1, group=group_calc, 
     tooltip="Number of bars used to calculate the linear regression line. Longer lengths provide a more comprehensive trend view.")

green = input.color(#00ffbb, title="Bullish Color", group=group_appearance, 
     tooltip="Color representing a strong bullish trend")

red = input.color(#ff1100, title="Bearish Color", group=group_appearance, 
     tooltip="Color representing a strong bearish trend")

// Linear Regression Calculation
v = ta.linreg(linreg_source, linreg_length, 0)

// Trend Calculation
trend = 0  

for i = 0 to n - 2 
    for j = i + 1 to n - 1
        if v[i] != v[j] 
            trend := trend + (v[i] > v[j] ? 1 : -1) 

totalcombos = (n*(n-1))/2

thresh = totalcombos*(p/100)

state = trend > thresh ? 1 : trend < -thresh ? -1 : 0

volatility = ta.atr(14)

// Dynamic Label with Intensity Visualization
label.new(bar_index, close > v ? v - volatility : v + volatility, style = label.style_none, textcolor = color.from_gradient(math.abs(trend)/totalcombos, 0, 1, color.new(trend > 0 ? green : red, 90), color.new(trend > 0 ? green : red, 30)), text = math.abs(trend) == totalcombos ? str.tostring(math.abs(trend)/totalcombos, "#.000000") : str.tostring(math.abs(trend)/totalcombos, "#.######"), force_overlay = true)

// Bar Coloring
barcolor(state == 0 ? color.gray : state == 1 ? green : red, title="Trend State Bar Color")

// Alert Conditions
alertcondition(state == 1, title="Bullish Trend Confirmed", message="Strong Bullish Trend Detected")

alertcondition(state == -1, title="Bearish Trend Confirmed", message="Strong Bearish Trend Detected")

alertcondition(state == 0, title="Trend Neutrality", message="Trend Intensity Reached Neutral Zone")