//@version=5
indicator(title='Chaikin Money Flow')

// Developed by Marc Chaikin, Chaikin Money Flow measures the amount of Money Flow Volume over a specific period.
// Money Flow Volume forms the basis for the Accumulation Distribution Line. Instead of a cumulative total of
// Money Flow Volume, Chaikin Money Flow simply sums Money Flow Volume for a specific look-back period, typically
// 20 or 21 days. The resulting indicator fluctuates above/below the zero line just like an oscillator. Chartists
// weigh the balance of buying or selling pressure with the absolute level of Chaikin Money Flow. Chartists can
// also look for crosses above or below the zero line to identify changes on money flow. 

// The Accumulation Distribution Line was developed by Marc Chaikin to measure the cumulative flow of money into and
// out of an index or security. The Accumulation/Distribution Line can be compared to the OBV (On Balance Volume),
// which adds or subtracts volume depending on the closing price. Marc Chaikin chose a different approach, instead
// of relying on the closing price, he used CLV (Close Location Value).

length = input.int(title='CMF Length', defval=20, minval=1)
smalen = input.int(title='SMA Length', defval=5, minval=1)
showma = input(title='Show SMA', defval=false)

clv = high == low ? 0 : (close - low - (high - close)) / (high - low)
mfv = clv * nz(volume, 1)

//adLine = cum(mfv) --Chaikin's Accum/Distrib Line
cmfLine = math.sum(mfv, length) / math.sum(nz(volume, 1), length)
smaLine = showma ? ta.sma(cmfLine, smalen) : na

cmfPlot = plot(cmfLine, 'Money Flow', color.black, linewidth=2)
smaPlot = plot(smaLine, 'Moving Avg', color.black, linewidth=1)
midLine = hline(0, 'Mid Line', color.black, linewidth=1, linestyle=hline.style_solid, editable=false)
zeroPlot = plot(0, color=na, display=display.none, editable=false)

fill(cmfPlot, zeroPlot, 0.0001, 0.0, color.new(color.green, 25), color.new(color.green, 100))
fill(cmfPlot, zeroPlot, 0.0, -0.0001, color.new(color.red, 100), color.new(color.red, 25))
