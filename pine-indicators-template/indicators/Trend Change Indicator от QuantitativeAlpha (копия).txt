// Â© QuantitativeAlpha
//@version=6
indicator(title = 'Trend Change Indicator', shorttitle = 'TCI', overlay = true)
fast_input = input.int(30, title = 'Fast Length', minval = 1)
slow_input = input.int(60, title = 'Slow Length', minval = 2)
strategy_type = input.string('EMA', title = 'Signal Type', options = ['EMA', 'SMA', 'RMA', 'WMA', 'VWMA'])
atr_length = input.int(60, title = 'ATR Length', minval = 1)
atr_margin = input.float(0.3, title = 'ATR Margin', minval = 0.0)
sma_input = input.int(140, title = 'SMA Length', minval = 1)
select_timeframe = input.string('4H', title = 'Timeframe', options = ['1H', '2H', '4H', '6H', '8H', '12H'])
normal_timeframe = select_timeframe == '1H' ? '60' : select_timeframe == '2H' ? '120' : select_timeframe == '4H' ? '240' : select_timeframe == '6H' ? '360' : select_timeframe == '8H' ? '480' : '720'
stocks_timeframe = select_timeframe == '1H' ? '30' : select_timeframe == '2H' ? '60' : select_timeframe == '4H' ? '120' : select_timeframe == '6H' ? '180' : select_timeframe == '8H' ? '240' : '360'
futures_timeframe = select_timeframe == '1H' ? '50' : select_timeframe == '2H' ? '100' : select_timeframe == '4H' ? '200' : select_timeframe == '6H' ? '300' : select_timeframe == '8H' ? '400' : '600'
timeframe = syminfo.type == 'crypto' ? normal_timeframe : syminfo.type == 'futures' ? futures_timeframe : syminfo.type == 'forex' ? futures_timeframe : stocks_timeframe
fast = strategy_type == 'SMA' ? ta.sma(close, fast_input) : strategy_type == 'WMA' ? ta.wma(close, fast_input) : strategy_type == 'RMA' ? ta.rma(close, fast_input) : strategy_type == 'VWMA' ? ta.vwma(close, fast_input) : ta.ema(close, fast_input)
slow = strategy_type == 'SMA' ? ta.sma(close, slow_input) : strategy_type == 'WMA' ? ta.wma(close, slow_input) : strategy_type == 'RMA' ? ta.rma(close, slow_input) : strategy_type == 'VWMA' ? ta.vwma(close, slow_input) : ta.ema(close, slow_input)
diff = fast - slow
bull = diff > atr_margin * ta.atr(atr_length)
bear = diff < -atr_margin * ta.atr(atr_length)
bullish = fast > slow
fast_hour = request.security(syminfo.tickerid, timeframe, ta.ema(close, fast_input))
slow_hour = request.security(syminfo.tickerid, timeframe, ta.ema(close, slow_input))
diff_hour = fast_hour - slow_hour
atr_hour = request.security(syminfo.tickerid, timeframe, ta.atr(atr_length))
bull_hour = diff_hour > atr_margin * atr_hour
bear_hour = diff_hour < -atr_margin * atr_hour
bullish_hour = fast_hour > slow_hour
sma = ta.sma(close, sma_input)
var is_long = false
no_trend = not bull and not bear
trend = bull or bear
is_change = not is_long and no_trend
is_no_change = is_long and trend
is_long := is_change ? true : is_no_change ? false : is_long
is_bull = no_trend[1] and bull
is_bear = no_trend[1] and bear
var is_long_hour = false
no_trend_hour = not bull_hour and not bear_hour
trend_hour = bull_hour or bear_hour
is_change_hour = not is_long_hour and no_trend_hour
is_no_change_hour = is_long_hour and trend_hour
is_long_hour := is_change_hour ? true : is_no_change_hour ? false : is_long_hour
is_bull_hour = no_trend_hour[1] and bull_hour
is_bear_hour = no_trend_hour[1] and bear_hour
clr_bull = color.new(color(#00FF00), 0)
clr_bull_fill = color.new(color(#00FF00), 50)
clr_bear = color.new(color(#FF0000), 0)
clr_bear_fill = color.new(color(#FF0000), 50)
clr_neutral = color.new(color(#FFFFFF), 0)
clr_neutral_fill = color.new(color(#FFFFFF), 50)
plot_fast = plot(fast, title = 'Fast Plot', linewidth = 2, color = bull ? clr_bull : bear ? clr_bear : clr_neutral)
plot_slow = plot(slow, title = 'Slow Plot', linewidth = 2, color = bull ? clr_bull : bear ? clr_bear : clr_neutral)
fill(plot_fast, plot_slow, title = 'Plot Fill', color = bull ? clr_bull_fill : bear ? clr_bear_fill : clr_neutral_fill)
plot(sma, title = 'SMA Line', color = color.new(color.white, 0), linewidth = 2)
var float[] forecasts = array.new_float(20, na)
for i = 1 to 20
    sum_past_closes = 0.0
    for j = 0 to i - 1
        sum_past_closes += close[sma_input - j]
    forecast = ((sma * sma_input) + ((close * i) - sum_past_closes)) / sma_input
    array.set(forecasts, i - 1, forecast)
plot(array.get(forecasts, 1), title = 'SMA Dot', color = color.new(color.white, 0), linewidth = 1, style = plot.style_circles, offset = 2, show_last = 1)
plot(array.get(forecasts, 4), title = 'SMA Dot', color = color.new(color.white, 0), linewidth = 1, style = plot.style_circles, offset = 5, show_last = 1)
plot(array.get(forecasts, 7), title = 'SMA Dot', color = color.new(color.white, 0), linewidth = 1, style = plot.style_circles, offset = 8, show_last = 1)
plot(array.get(forecasts, 10), title = 'SMA Dot', color = color.new(color.white, 0), linewidth = 1, style = plot.style_circles, offset = 11, show_last = 1)
plot(array.get(forecasts, 13), title = 'SMA Dot', color = color.new(color.white, 0), linewidth = 1, style = plot.style_circles, offset = 14, show_last = 1)
plot(array.get(forecasts, 16), title = 'SMA Dot', color = color.new(color.white, 0), linewidth = 1, style = plot.style_circles, offset = 17, show_last = 1)
plot(array.get(forecasts, 19), title = 'SMA Dot', color = color.new(color.white, 0), linewidth = 1, style = plot.style_circles, offset = 20, show_last = 1)
var table t = table.new(position.bottom_right, 1, 3, border_width = 2)
table.cell(t, 0, 2, text = '', width = 14, text_color = color.new(color.white, 100), bgcolor = color.new(color.white, 100))
table.cell(t, 0, 0, text = bullish_hour ? 'BULL' : 'BEAR', width = 14, text_color = color.white, bgcolor = bull_hour ? color.green : bear_hour ? color.red : color.gray)
table.cell(t, 0, 1, text = bullish ? 'BULL' : 'BEAR', width = 14, text_color = color.white, bgcolor = bull ? color.green : bear ? color.red : color.gray)
alertcondition(is_change, title = 'Trend Change', message = 'Warning! Trend Change!')
alertcondition(is_bull, title = 'Trend Bullish', message = 'Warning! Trend Bullish!')
alertcondition(is_bear, title = 'Trend Bearish', message = 'Warning! Trend Bearish!')
alertcondition(is_change_hour, title = 'Hourly Trend Change', message = 'Warning! Hourly Trend Change!')
alertcondition(is_bull_hour, title = 'Hourly Trend Bullish', message = 'Warning! Hourly Trend Bullish!')
alertcondition(is_bear_hour, title = 'Hourly Trend Bearish', message = 'Warning! Hourly Trend Bearish!')