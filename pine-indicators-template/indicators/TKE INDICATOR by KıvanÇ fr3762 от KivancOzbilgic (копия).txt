//@version=5
//CREATOR: DR.YASAR ERDINC @yerdinc65
//AUTHOR: KIVANC @kivancozbilgic
indicator('TKE')
period = input.int(14, title='period', minval=1, maxval=500)
emaperiod = input.int(5, title='EMAperiod', minval=1, maxval=500)
novolumedata = input(title='Exclude Volume in Calculation?', defval=false)
EMA = input(false, title='Show EMA Line?')
momentum = close / close[period] * 100
cci = ta.cci(hlc3, period)
rsi = ta.rsi(close, period)
willr = (ta.highest(high, period) - close) / (ta.highest(high, period) - ta.lowest(low, period)) * -100
stosk = ta.stoch(close, high, low, period)
upper_s = math.sum(volume * (ta.change(hlc3) <= 0 ? 0 : hlc3), period)
lower_s = math.sum(volume * (ta.change(hlc3) >= 0 ? 0 : hlc3), period)
mfi = 100.0 - 100.0 / (1.0 + upper_s / lower_s)


spacing = input(7)
length7 = input.int(7, minval=1)
length14 = input.int(14, minval=1)
length28 = input.int(28, minval=1)
average(bp, tr_, length) =>
    math.sum(bp, length) / math.sum(tr_, length)
high_ = math.max(high, close[1])
low_ = math.min(low, close[1])
bp = close - low_
tr_ = high_ - low_
avg7 = average(bp, tr_, length7)
avg14 = average(bp, tr_, length14)
avg28 = average(bp, tr_, length28)
ult = 100 * (4 * avg7 + 2 * avg14 + avg28) / 7


TKEline = novolumedata ? (ult + momentum + cci + rsi + willr + stosk) / 6 : (ult + mfi + momentum + cci + rsi + willr + stosk) / 7
EMAline = ta.ema(TKEline, emaperiod)
plot(EMA and EMAline ? EMAline : na, title='EMA Line', style=plot.style_line, linewidth=2, color=color.new(color.blue, 0))
plot(TKEline, color=color.new(color.red, 0), linewidth=3, title='TKE')
plot(20, color=color.new(color.blue, 0), linewidth=0, title='')
plot(80, color=color.new(color.red, 0), linewidth=0, title='')
plot(0, color=color.new(color.purple, 0), linewidth=0, title='')

