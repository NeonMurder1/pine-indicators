// Â© QuantitativeAlpha
//@version=6
indicator(title = 'Extreme Reversal Signal', shorttitle = 'ERS', overlay = true)
bb_input = input.int(20, title = 'MA Length', minval = 1)
basis_type = input.string('SMA', title = 'Basis Type', options = ['EMA', 'SMA', 'RMA', 'WMA', 'VWMA'])
std_dev_up = input.float(2.0, title = 'SD Upper', minval = 0.0)
std_dev_down = input.float(2.0, title = 'SD Lower', minval = 0.0)
sto_input = input.int(14, title = 'STO Length', minval = 1)
upper_limit_sto = input.int(70, title = 'STO Upper', maxval = 100)
lower_limit_sto = input.int(30, title = 'STO Lower', minval = 1)
rsi_input = input.int(14, title = 'RSI Length', minval = 1)
upper_limit_rsi = input.int(70, title = 'RSI Upper', maxval = 100)
lower_limit_rsi = input.int(30, title = 'RSI Lower', minval = 1)
basis = basis_type == 'SMA' ? ta.sma(close, bb_input) : basis_type == 'RMA' ? ta.rma(close, bb_input) : basis_type == 'WMA' ? ta.wma(close, bb_input) : basis_type == 'VWMA' ? ta.vwma(close, bb_input) : ta.ema(close, bb_input)
dev = ta.stdev(close, bb_input)
upper = basis + (std_dev_up * dev)
lower = basis - (std_dev_down * dev)
rsi = ta.rsi(close, rsi_input)
stoch = ta.stoch(rsi, rsi, rsi, sto_input)
sma = ta.sma(stoch, 3)
bull = close[1] < lower[1] and close > lower and sma[1] < lower_limit_sto
bear = close[1] > upper[1] and close < upper and sma[1] > upper_limit_sto
plotshape(bull, title = 'Buy', style = shape.triangleup, location = location.belowbar, color = color.green, size = size.small)
plotshape(bear, title = 'Sell', style = shape.triangledown, location = location.abovebar, color = color.red, size = size.small)
z_score = (close - basis) / dev
z_score_text = str.tostring(z_score, '#.#')
z_score_bull = z_score < -std_dev_down
z_score_bear = z_score > std_dev_up
rsi_text = str.tostring(rsi, '#')
rsi_bull = rsi < lower_limit_rsi
rsi_bear = rsi > upper_limit_rsi
var table t = table.new(position.bottom_right, 1, 3, border_width = 2)
table.cell(t, 0, 2, text = '', width = 14, text_color = color.new(color.white, 100), bgcolor = color.new(color.white, 100))
table.cell(t, 0, 0, text = rsi_text, width = 14, text_color = color.white, bgcolor = rsi_bull ? color.green : rsi_bear ? color.red : color.gray)
table.cell(t, 0, 1, text = z_score_text, width = 14, text_color = color.white, bgcolor = z_score_bull ? color.green : z_score_bear ? color.red : color.gray)
alertcondition(bull, title = 'Buy Signal', message = 'Warning! Buy Signal!')
alertcondition(bear, title = 'Sell Signal', message = 'Warning! Sell Signal!')