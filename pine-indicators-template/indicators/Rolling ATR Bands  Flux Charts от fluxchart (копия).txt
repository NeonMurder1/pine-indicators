// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © fluxchart
//@version=5

indicator("Rolling ATR Bands | Flux Charts", overlay = true, max_labels_count = 500)

atrLen = input.int(10, "ATR Length", group = "General Configuration")
atrMultiplier = input.float(1.618, "ATR Multiplier", group = "General Configuration")
smoothing = input.int(10, "Trend Smoothing", group = "General Configuration", minval = 1, tooltip = "Higher settings will result in longer periods of time required for trend to change direction from bullish to bearish and vice versa.", display = display.none)
showBreakLabels = input.bool(true, "Show Break Labels", group = "General Configuration")
showRetestLabels = input.bool(true, "Show Retest Labels", group = "General Configuration")
EMA1Length = 9
EMA2Length = 21

bearishColor = input.color(#f23646, "Bearish", group = "Colors", inline = "col", display = display.none)
neutralColor = input.color(color.gray, "Neutral", group = "Colors", inline = "col", display = display.none)
bullishColor = input.color(#089981, "Bullish", group = "Colors", inline = "col", display = display.none)
textColor = input.color(color.white, "Text", group = "Colors", inline = "col", display = display.none)
breakLabelColor = input.color(color.blue, "Break", group = "Colors", inline = "col2", display = display.none)

const int minLabelDistance = 2
var int lastLowLabel = 0
var int lastHighLabel = 0

const float rsiEff = 0.5
const float supertrendFactor = 3.0
const float supertrendEff = 0.2

float emaATRRange = (EMA2Length - EMA1Length) < 20 ? 1.6 : (EMA2Length - EMA1Length) < 50 ? 2 : 3
const float emaCrossEff = 0.3

var label trendLabel = na

f_lin_interpolate(float x0, float x1, float y0, float y1, float x) =>
    y0 + (x - x0) * (y1 - y0) / (x1 - x0)

atr = ta.atr(atrLen)

curRSI = ta.rsi(close, atrLen)
curRSI := f_lin_interpolate(15, 85, -100, 100, curRSI)

[supertrend, supDirection] = ta.supertrend(supertrendFactor, atrLen)
curSupertrend = f_lin_interpolate(-1, 1, -100, 100, -supDirection)

ema1 = ta.ema(close, EMA1Length)
ema2 = ta.ema(close, EMA2Length)

emaDiff = ema1 - ema2
emaDiff := f_lin_interpolate(-atr * emaATRRange, atr * emaATRRange, -100, 100, emaDiff)

totalStrengthRaw = (curRSI * rsiEff) + (curSupertrend * supertrendEff) + (emaDiff * emaCrossEff)
totalStrength = ta.ema(totalStrengthRaw, smoothing)

curColor = color.from_gradient(totalStrength, -100, 0, bearishColor, neutralColor)
if totalStrength > 0
    curColor := color.from_gradient(totalStrength, 15, 85, neutralColor, bullishColor)
//barcolor(curColor)

upperBand = ta.rma(close + atr * atrMultiplier, 3)
lowerBand = ta.rma(close - atr * atrMultiplier, 3)

upperBandColor = curColor
lowerBandColor = upperBandColor

p1 = plot(upperBand, title="Upper Band", color=upperBandColor, linewidth=2)
p2 = plot(lowerBand, title="Lower Band", color=lowerBandColor, linewidth=2)

fill(p1, p2, color = color.new(upperBandColor, 75))

renderRetestLabelHigh = false
renderBreakLabelHigh = false
renderRetestLabelLow = false
renderBreakLabelLow = false

if barstate.isconfirmed
    if close > upperBand
        renderBreakLabelHigh := true
    else if high > upperBand
        renderRetestLabelHigh := true
    
    if close < lowerBand
        renderBreakLabelLow := true
    else if low < lowerBand
        renderRetestLabelLow := true

    if showRetestLabels and renderRetestLabelHigh and (bar_index - lastHighLabel) > minLabelDistance
        label.new(bar_index, close, style = label.style_label_down, yloc = yloc.abovebar, color = color.new(bearishColor, 50), text = "R", textcolor = textColor, size = size.small)
        lastHighLabel := bar_index
    if showBreakLabels and renderBreakLabelHigh and (bar_index - lastLowLabel) > minLabelDistance
        label.new(bar_index, close, style = label.style_label_up, yloc = yloc.belowbar, color = color.new(breakLabelColor, 30), text = "B", textcolor = textColor, size = size.small)
        lastLowLabel := bar_index
    
    if showRetestLabels and renderRetestLabelLow and (bar_index - lastLowLabel) > minLabelDistance
        label.new(bar_index, close, style = label.style_label_up, yloc = yloc.belowbar, color = color.new(bullishColor, 50), text = "R", textcolor = textColor, size = size.small)
        lastLowLabel := bar_index
    if showBreakLabels and renderBreakLabelLow and (bar_index - lastHighLabel) > minLabelDistance
        label.new(bar_index, close, style = label.style_label_down, yloc = yloc.abovebar, color = color.new(breakLabelColor, 30), text = "B", textcolor = textColor, size = size.small)
        lastHighLabel := bar_index

alertcondition(renderRetestLabelHigh, "Upper Band Retest", "")
alertcondition(renderBreakLabelHigh, "Upper Band Broken", "")

alertcondition(renderRetestLabelLow, "Lower Band Retest", "")
alertcondition(renderBreakLabelLow, "Lower Band Broken", "")
