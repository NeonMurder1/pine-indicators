// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© LonesomeTheBlue
 
//@version=4
study("Smoothed Candles", overlay = true)
mode =input(title = "Method", defval = 'Dynamic', options=['User Defined', 'Dynamic', 'Percentage'])
userdefined = input(title ="[UserDefined] Tick", defval = 10., type = input.float, minval = 0.0000000000001)
atrlen = input(title ="[Dynamic] Atr Period", defval = 14, minval = 1)
percentagebs = input(title ="[Percentage] Percentage %", defval = 0.1, minval = 0.00001, step = 0.1)
showma1 = input(defval = false, title = "Show MA 1")
ma1len = input(defval = 50, title = "MA 1 Length", minval = 1)
showma2 = input(defval = false, title = "Show MA 2")
ma2len = input(defval = 200, title = "MA 2 Length", minval = 1)
showema1 = input(defval = false, title = "Show EMA 1")
ema1len = input(defval = 50, title = "EMA 1 Length", minval = 1)
showema2 = input(defval = false, title = "Show EMA 2")
ema2len = input(defval = 200, title = "EMA 1 Length", minval = 1)
showwick = input(defval = true, title = "Show Wicks")
colorup = input(defval = color.lime, title = "Body", inline = "bcol")
colordown = input(defval = color.red, title = "", inline = "bcol")
bcolup = input(defval = #74e05e, title = "Border", inline = "bocol")
bcoldown = input(defval = #ffad7d, title = "", inline = "bocol")
wcol = input(defval = #b5b5b8, title = "Wicks")

roundtotick(price)=> round(price / syminfo.mintick) * syminfo.mintick

float rclose = roundtotick(close)
dynamicbox = atr(atrlen)
percentbox = roundtotick(percentagebs * rclose / 100)

float box = na
box := na(box[1]) ? mode == 'Dynamic' ? dynamicbox : mode == 'Percentage' ? percentbox : userdefined * syminfo.mintick : box[1] 
if na(box)
    box := syminfo.mintick

var float iopen = na
var float ihigh = na
var float ilow = na
var float iclose = roundtotick(close / box) * box
float num = round((rclose - iclose) / box)
if num != 0
    iopen := iclose
    iclose := iclose + num * box
    ihigh := high
    ilow := low
else 
    if not na(ihigh)
        ihigh := max(ihigh, high)
        ilow := min(ilow, low)

// set box size if close price changed
box :=  change(iclose) ? mode == 'Dynamic' ? dynamicbox : mode == 'Percentage' ? percentbox : userdefined * syminfo.mintick : box

plotcandle(iopen, showwick ? ihigh : max(iclose, iopen), showwick ? ilow : min(iclose, iopen), iclose, title='Smoothed Candles', 
          color = iopen <= iclose ? colorup : colordown, 
          wickcolor = wcol, 
          bordercolor = iopen <= iclose ? bcolup : bcoldown)

plot(showma1 ? sma(iclose, ma1len) : na, title = "MA 1", color = color.lime)
plot(showma2 ? sma(iclose, ma2len) : na, title = "MA 2", color = color.red)
plot(showema1 ? ema(iclose, ema1len) : na, title = "EMA 1", color = color.lime)
plot(showema2 ? ema(iclose, ema2len) : na, title = "EMA 2", color = color.red)
